plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.18.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile 'com.jfoenix:jfoenix:9.0.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

javafx {
    version = "13"
    modules = ['javafx.controls', 'javafx.fxml']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

mainClassName = 'incamoon.MainApp'

def buildProperties = new Properties()
file("src/main/resources/version.properties").withInputStream {
    stream -> buildProperties.load(stream)
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'esoAddonDeps'
        version = buildProperties.getProperty("version")
        jvmArgs = ['']
    }
    jpackage {
        imageOptions = ['--resource-dir', "${projectDir}\\jpackage", '--verbose']
        installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--type', 'msi', '--win-shortcut', '--win-menu', '--win-menu-group', 'fr33t00lz']
    }
}